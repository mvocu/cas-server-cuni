import org.apache.tools.ant.taskdefs.condition.*
import org.gradle.internal.logging.text.*
import org.apereo.cas.metadata.*
import java.nio.file.*
import static org.gradle.internal.logging.text.StyledTextOutput.Style

buildscript {
    repositories {
        if (project.privateRepoUrl) {
          maven {
            url project.privateRepoUrl
            credentials {
              username = project.privateRepoUsername
              password = System.env.PRIVATE_REPO_TOKEN
            }
          }
        }
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
            mavenContent { snapshotsOnly() }
        }
        maven {
            url "https://repo.spring.io/milestone"
            mavenContent { releasesOnly() }
        }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${project.springBootVersion}"
        classpath "io.freefair.gradle:maven-plugin:${project.gradleFreeFairPluginVersion}"
        classpath "io.freefair.gradle:lombok-plugin:${project.gradleFreeFairPluginVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:${project.gradleDependencyManagementPluginVersion}"
        classpath "com.google.cloud.tools:jib-gradle-plugin:${project.jibVersion}"

        classpath "de.undercouch:gradle-download-task:${project.gradleDownloadTaskVersion}"
        classpath "org.apereo.cas:cas-server-core-api-configuration-model:${project.'cas.version'}"
        classpath "org.apereo.cas:cas-server-core-configuration-metadata-repository:${project.'cas.version'}"
    }
}

repositories {
    if (project.privateRepoUrl) {
      maven {
        url project.privateRepoUrl
        credentials {
          username = project.privateRepoUsername
          password = System.env.PRIVATE_REPO_TOKEN
        }
      }
    }
    mavenLocal()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/releases' }
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
        mavenContent { snapshotsOnly() }
    }
    maven { url 'https://build.shibboleth.net/nexus/content/repositories/releases/' }
    maven {
        url "https://repo.spring.io/milestone"
        mavenContent { releasesOnly() }
    }
}

apply plugin: "io.freefair.war-overlay"
apply plugin: "war"
apply plugin: "org.springframework.boot"
apply plugin: "io.freefair.lombok"

apply from: rootProject.file("gradle/springboot.gradle")
apply from: rootProject.file("gradle/jib.gradle")
apply from: rootProject.file("gradle/tasks.gradle")


configurations.all {

    exclude(group: "org.apache.logging.log4j", module: "log4j-api")
    exclude(group: "org.apache.logging.log4j", module: "log4j-web")
    exclude(group: "org.apache.logging.log4j", module: "log4j-jcl")
    exclude(group: "org.apache.logging.log4j", module: "log4j-slf4j-impl")

    exclude(group: "org.apereo.cas", module: "cas-server-core-logging")

    resolutionStrategy {
        cacheChangingModulesFor 0, "seconds"
        cacheDynamicVersionsFor 0, "seconds"
        preferProjectModules()
        def failIfConflict = project.hasProperty("failOnVersionConflict") && Boolean.valueOf(project.getProperty("failOnVersionConflict"))
        if (failIfConflict) {
            failOnVersionConflict()
        }
    }
}

war {
    entryCompression = ZipEntryCompression.STORED
    enabled = false
}

sourceSets {
    bootRunSources {
        resources {
            srcDirs new File("//etc/cas/templates/"), new File("${project.getProjectDir()}/src/main/resources/")
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(project.targetCompatibility)
    }
}

bootBuildImage {
    imageName = "${project.'containerImageOrg'}/${project.'containerImageName'}:${project.version}"
}

dependencies {
    /**
    * Do NOT modify the lines below or else you will risk breaking dependency management.
    */
    implementation enforcedPlatform("org.apereo.cas:cas-server-support-bom:${project.'cas.version'}")
    implementation platform(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)

    /**
     * CAS dependencies and modules may be listed here.
     *
     * There is no need to specify the version number for each dependency
     * since versions are all resolved and controlled by the dependency management
     * plugin via the CAS bom.
     **/

    implementation "org.apereo.cas:cas-server-core-api-configuration-model"
    implementation "org.apereo.cas:cas-server-webapp-init"

    if (project.hasProperty("casModules")) {
        def dependencies = project.getProperty("casModules").split(",")
        dependencies.each {
            def projectsToAdd = rootProject.subprojects.findAll {project ->
                project.name == "cas-server-core-${it}" || project.name == "cas-server-support-${it}"
            }
            projectsToAdd.each {implementation it}
        }
    }
    
    implementation "org.apereo.cas:cas-server-core-events-configuration:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-webapp-init:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-support-logback:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-support-json-service-registry:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-support-ldap:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-support-jdbc:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-support-hazelcast-ticket-registry:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-support-saml:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-support-oidc:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-support-oauth-webflow:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-support-pac4j-webflow:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-support-shibboleth:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-support-saml-mdui:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-support-webauthn:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-support-webauthn-ldap:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-support-gauth:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-support-gauth-ldap:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-support-simple-mfa:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-support-trusted-mfa:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-support-trusted-mfa-jdbc:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-support-spnego-webflow:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-support-pac4j-webflow:${project.'cas.version'}"

    /* for spnego */
    implementation files("${projectDir}/lib/jcifs-ext.jar")
    /* for eidas extension to saml2 */
    implementation 'se.litsec.eidas:eidas-opensaml4:2.1.2'
    /* for running on jetty */
    implementation "org.eclipse.jetty:jetty-util:10.0.7"

    developmentOnly "org.springframework.boot:spring-boot-devtools:${project.springBootVersion}"

    compileOnly "org.springframework.boot:spring-boot:${project.springBootVersion}"

    compileOnly "org.apereo.cas:cas-server-support-simple-mfa-core"
    compileOnly "org.apereo.cas:cas-server-support-webauthn-core"
    compileOnly "org.apereo.cas:cas-server-support-spnego"
    compileOnly "org.apereo.cas:cas-server-core-authentication-api"
    compileOnly "org.apereo.cas:cas-server-core-configuration-api"
    compileOnly "org.apereo.cas:cas-server-support-pac4j-core-clients"

   /* these are for yet uncommited changes */
    compileOnly "org.apereo.cas:cas-server-support-webauthn"
    compileOnly "org.apereo.cas:cas-server-core-util"
    compileOnly "org.apereo.cas:cas-server-core-web-api"
    compileOnly "org.apereo.cas:cas-server-support-webauthn-core-webflow"
    compileOnly "org.apereo.cas:cas-server-core-webflow-api"
    compileOnly "org.apereo.cas:cas-server-core-authentication-mfa-api"
    compileOnly "org.springframework.security:spring-security-config"

}

